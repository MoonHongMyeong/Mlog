buildscript{
	ext{
		queryDslVersion = "4.4.0"
	}
}

plugins {
	id 'org.springframework.boot' version '2.4.3-SNAPSHOT'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'com.github.node-gradle.node' version '2.2.4'
	id 'java'
}
//node plugin
apply plugin: 'com.github.node-gradle.node'

group = 'me.portpolio'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'
configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

dependencies {
	//기본적인 스프링부트 웹, 테스트
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	//JPA 추가
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	//devtools 추가
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	//로컬 테스트용 h2 jdbc 추가
	runtimeOnly 'com.h2database:h2'
	//mysql jdbc 추가
	runtimeOnly 'mysql:mysql-connector-java'
	//lombok 추가
	annotationProcessor 'org.projectlombok:lombok'
	compileOnly 'org.projectlombok:lombok'
	//unit test 를 위한 google truth 추가 구버전의 assertion 을 쓸 수 있다.
	testImplementation 'com.google.truth:truth:1.1'
	//querydsl 추가
	implementation ("com.querydsl:querydsl-jpa:${queryDslVersion}")
	annotationProcessor ("com.querydsl:querydsl-apt:${queryDslVersion}:jpa")
	testImplementation ("com.querydsl:querydsl-jpa:${queryDslVersion}")
	testAnnotationProcessor ("com.querydsl:querydsl-apt:${queryDslVersion}:jpa")
	//querydsl 추가를 그냥하면 문제가 생긴다 java.lang.NoClassDefFoundError : javax/annotation/Generated
	//javax-annotation-api 의존성을 추가
	implementation("javax.annotation:javax.annotation-api:1.3.2")
	annotationProcessor("javax.annotation:javax.annotation-api:1.3.2")

}

//querydsl Qclass 생성하는 장소 추가
def generated='src/main/generated'
sourceSets {
	main.java.srcDirs += [ generated ]
}
tasks.withType(JavaCompile) {
	options.annotationProcessorGeneratedSourcesDirectory = file(generated)
}
clean.doLast {
	file(generated).deleteDir()
}

//그레이들 빌드 시 노드 부분 빌드하고 같이 빌드함
def webappDir = '$projectDir/frontend/blog-frontend'
node{
	//개발용 노트북 버전
//	version='14.15.2'
//	npmVersion='6.14.9'


	//EC2 인스턴스 버전
	version = '15.8.0'
	npmVersion='7.5.1'
	download = true
	workDir = file('${project.projectDir}/frontend/blog-frontend')
	npmWorkDir = file("${project.buildDir}/npm")
}

task appNpmInstall(type: NpmTask){
	workingDir = file('${project.projectDir}/frontend/blog-frontend')
	args=['install']
}
task yarnBuild(type: YarnTask){
	workingDir = file('${project.projectDir}/frontend/blog-frontend')
	args=['build']
}
task copyWebApp(type: Copy){
	from "frontend/blog-frontend/build"
	into 'build/resources/main/static/.'
}

yarnBuild.dependsOn appNpmInstall
copyWebApp.dependsOn yarnBuild
compileJava.dependsOn copyWebApp

test {
	useJUnitPlatform()
}
